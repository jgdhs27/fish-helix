#!/usr/bin/env fish

set root (dirname (status filename))
set -l error false

function do_test -a test_file
    set tempdir "$(mktemp -d)"
    mkfifo "$tempdir/fifo"
    # FIXME on `-v` echo "$test_file"
    fish "$root/tests/_run.fish" "$test_file" "$tempdir"
end

do_test "$root/tests/_example_test.fish" 2>/dev/null
or begin
    echo "$root/tests/_example_test.fish has failed, but it should have succeeded" >&2
    set error true
end

do_test "$root/tests/_broken_test.fish" 2>/dev/null
and begin
    echo "$root/tests/_broken_test.fish has succeeded, but it should have failed" >&2
    set error true
end

if test "$error" = true
    exit 1
end

set passed 0
set failed 0
set broken 0

if test (count $argv) = 0
    set argv "$root/tests"
end
for dir in $argv
    find "$dir" -type f -name "[^_]*.fish" | while read -l -d\n filename
        do_test "$filename"
        switch $status
            case 0
                set passed (math "$passed" + 1)
            case 1
                set failed (math "$failed" + 1)
            case 2
                set broken (math "$broken" + 1)
        end
    end
end

echo "$(math "$passed" + "$broken" + "$failed") tests finished. $failed failed and $broken broken" >&2
test "$failed" = 0
